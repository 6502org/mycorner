<script type="text/javascript" src="http://ads.tripod.lycos.co.uk/ad/tripodbar/framejs.php?cat=memberpages.other&CC=uk&ord=7c98a044&adpref=&gg_bg=&gg_template="></script>
<HTML>
<HEAD>
<TITLE>Enhanced 68k BASIC, startrek.bas</TITLE>
<META name="description" content="An advanced BASIC interpreter for the 68k microprocessor
 family.">
</HEAD>
<BODY BACKGROUND="../../back.png" LINK="#0000EE" VLINK="#0000CC">
<TABLE BORDER=0 WIDTH=100% CELLSPACING=0 CELLPADDING=0>
<TR><TD WIDTH=20%><A HREF="examples.html#link_1">
 <IMG SRC="../../up.gif" BORDER=0 ALT="Up one"></A></TD>
<TD WIDTH=60% ALIGN=CENTER>
<B><FONT SIZE=+1>startrek.bas. </FONT></B>
<FONT SIZE=-1>By Lee Davison.</FONT>
</TD><TD WIDTH=20%><A HREF="../../index.html">
 <IMG SRC="../../epc.png" ALIGN=RIGHT BORDER=0 ALT="Up to top"></A>
</TD></TR></TABLE>
<HR>
Download <A HREF="startrek.zip">zipped <IMG SRC="../../zip_sm.png" border=0></A>.
<P>
<U><B>Introduction</B></U>
<BLOCKQUOTE><P ALIGN=JUSTIFY>
 A quick port of a classic game. I used to spend many a happy hour killing Klingons on
 the schools RML 380Z a few (ahem!) years ago. This has been modified to use some of
 the features of EhBASIC and to remove some of the <I>undocumented features</I> of the
 original. If anything doesn't seem to work properly let me know.
<P ALIGN=JUSTIFY>
 If you keep getting killed you can cheat by just hitting <B>ENTER</B> at the 'Command?'
 prompt. Typing ..
<BLOCKQUOTE>
 <B>S=1E10:E=1E10:CONT</B>
</BLOCKQUOTE><P ALIGN=JUSTIFY>
 .. and hitting <B>ENTER</B> again. This will give enough shields and energy for the most
 inept Captain to defeat the Klingons.
</BLOCKQUOTE>
<U><B>startrek.bas</B></U>
<BLOCKQUOTE>
<TABLE CELLPADDING=8 CELLSPACING=0 BORDER=1 BGCOLOR=white><TR><TD>
<PRE>

10 REM EXPANDED APRIL 1977 BY W.A. BURTON
20 REM MODIFIED MARCH 2003 FOR EhBASIC BY
25 REM L. DAVISON leeedavison@lycos.co.uk
30 REM
35 DEF FNA1(X)=INT(A1+X*COS(C1*CX)+.5)
40 DEF FNB1(X)=INT(B1+X*SIN(C1*CX)+.5)
45 DIM G(8,8),S(8,8),K(3,3),D$(10),A$(10),D(10),C2(10),C3(10)
50 C$=""
55 T=0:KP=T:DT=T:S=T:KA=T:SL=T:CY=T:KQ=T
60 GOSUB 5470
65 PRINT
70 INPUT "What is your security clearance number (1 TO 1000)";T9
80 E8=T9
90 IF (T9<1) OR (T9>1000) THEN PRINT AR$;"INVALID!! REENTER!!":GOTO 70
100 INPUT"       Enter rank     (1=Low,12=High)";R9
110 IF (R9<1) OR (R9>12) THEN 100
120 R9=RND(T9*R9)
140 RR=R9
150 X4=R9:R9=R9+1E-03:W1=W1+1E-03
170 PRINT "Command order : STAND BY !"
180 PRINT "You are presently being assigned to a mission..."
190 PRINT " with a failure factor of"; RR*20
200 PRINT"       Good-bye, Captain..."
210 CX=TWOPI/360:AR$=" ---> "
220 XA=INT(X4*50):AC=(1/(X4+.1))
230 IF X4>=9 THEN 3800
240 E=3000-XA+100:P=10:S9=200
250 DEF FND(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
260 Q1=INT(RND(0)*8+1):Q2=INT(RND(0)*8+1)
270 S1=INT(RND(0)*8+1):S2=INT(RND(0)*8+1)
280 DATA "Warp Engines","Short Range Sensors","Long Range Sensors"
290 DATA "Phasers", "Photon Torpedoes","Shield Control"
300 DATA "Damage Control", "Computer","Damage Repair","Communications"
310 RESTORE:FOR A=1 TO 10:READ D$(A):NEXT A
320 A$(0)=" . ":A$(1)=" E ":A$(2)=" * ":A$(3)=" K ":A$(4)=" B "
330 F1=.86:F2=.01:F3=.95:F4=.99:K9=0:B9=K9
340 FOR I=1 TO 8:FOR J=1 TO 8:R1=RND(0):R2=RND(0):R3=INT(8*RND(0)+1)
350 K3=-(R1>F1-F2*R9)-(R1>F3-F2*R9)-(R1>F4-F2*R9):K9=K9+K3
360 B3=-(R2>F3):B9=B9+B3:G(I,J)=100*K3+10*B3+R3:NEXT J: NEXT I
370 IF NOT((B9>0) AND (K9>0))THEN 330
380 PRINT"Objective: Destroy";K9;" Klingons using";B9;" starbase";
390 U=K9:Y=B9
400 IF B9<>1 THEN PRINT"s";
420 T9=K9+RND(0)*K9/R9+10-R9:PR=T9/K9:PRINT" in";T9;" stardays."
430 GOSUB 4270
440 S3=0:B3=S3:K3=B3:FOR A=1 TO 8:FOR B=1 TO 8:S(A,B)=0:NEXT B: NEXT A
450 FOR A=1 TO 3:FOR B=1 TO 3:K(A,B)=0:NEXT B:NEXT A:S(S1,S2)=1
460 X=.01*G(Q1,Q2):K3=INT(X):Y=(X-K3)*10:B3=INT(Y)
470 S3=G(Q1,Q2)-100*K3-10*B3
480 IF K3=0 THEN FOR A=1 TO 3:FOR B=1 TO 3:K(A,B)=0:NEXT B:NEXT A:GOTO 520
490 PRINT AR$;"CONDITION RED !!!!!":FOR A=1 TO K3
500 R1=INT(RND(0)*8+1):R2=INT(RND(0)*8+1):IF S(R1,R2) THEN 500
510 S(R1,R2)=3:K(A,1)=R1:K(A,2)=R2:K(A,3)=S9:NEXT A
520 IF B3=0 THEN 550
530 R1=INT(RND(0)*8+1):R2=INT(RND(0)*8+1):IF S(R1,R2) THEN 530
540 S(R1,R2)=4
550 IF S3=0 THEN 590
560 FOR A=1 TO S3
570 R1=INT(RND(0)*8+1):R2=INT(RND(0)*8+1):IF S(R1,R2) THEN 570
580 S(R1,R2)=2:NEXT A
590 G(Q1,Q2)=INT(G(Q1,Q2))+.5

REM PRESENT POSITION

600 PRINT
610 PRINT"QUADRANT (";Q1;",";Q2;")"
620 PRINT"SECTOR   (";S1;",";S2;")"
630 IF KP THEN 4060
640 IF DT=1 THEN 2680
650 IF S<200*K3 THEN PRINT AR$;"Shield Energy too low":SL=1:A=6:GOTO 750
660 A=2: GOTO 750

REM SHORT RANGE SCAN

670 PRINT
680 PRINT D$(2):FOR A=1 TO 8:FOR B=1 TO 8:PRINT A$(S(A,B));:NEXT B:PRINT:NEXT A
690 PRINT:PRINT"Shields";S;SPC(3);" Energy";E;SPC(3);" Photons";P;
700 PRINT SPC(3);" Stardays";T9-T

710 GOSUB 3220:INPUT"Command";A
720 IF (A>10) OR (A<0) THEN INPUT" ----> COMMAND";A
730 IF (A>10) OR (A<0) THEN PRINT"WARNING - PROPER COMMANDS ONLY !! ":GOTO 710
740 IF A=1 THEN 780
750 IF D(A)>=0 THEN 780
760 PRINT AR$;"Captain, we don't have ";D$(A);" anymore."
770 IF DT=1 THEN 2680
775 GOTO 710
780 ON A GOTO 820,670,1470,1530,1800,2140,2230,2680,4910,3910
790 PRINT:FOR A=1 TO 9:PRINT A;" = ";D$(A):NEXT A
800 A=10
810 PRINT A;"= ";D$(A):GOTO 710

REM WARP ENGINES

820 INPUT"COURSE";C1:IF (C1<0) OR (C1>359.99) THEN 850
830 INPUT"WARP FACTOR";W1
840 SL=0:KP=0:GOTO 860
850 PRINT AR$"REJECTED ! Course must be in range of 0 to 359.99 degrees."
860 IF NOT(W1>0)THEN 710
870 IF (D(1)>=0) OR (W1<=AC) THEN 900
880 PRINT AR$;"Warp Engines are damaged, maximum speed =";AC
890 GOTO 820
900 INC TEC:C$="":IF K3>0 THEN GOSUB 2290
910 IF E>5*W1 THEN 980
920 IF S<1 THEN 2450
930 PRINT"Captain, you only have";E;" units of energy. "
940 PRINT"Refuel from your Shield reserves, which have";S;" units";
950 INPUT " ";Q9$:IF UCASE$(LEFT$(Q9$,1))="Y" THEN GOSUB 6860
960 A=6:IF D(A)<0 THEN 2450
970 GOTO 710
980 FOR I=1 TO 10:IF D(I)>=0 THEN 1020
990 INC D(I):IF D(I)<0 THEN 1020
1000 IF D(7)<0 THEN 1020
1010 PRINT"Damage Control report: ";D$(I);" repaired."
1020 NEXT I:IF RND(0)>.1 THEN 1130
1030 IF RND(0)>.1 THEN 1130
1040 GOTO 1100
1050 IF RND(0)>R9/10 THEN RETURN
1060 R1=INT(RND(0)*8+1):IF D(R1)<0 THEN RETURN
1070 D(R1)=D(R1)-10*RND(0)-1:IF D(7)<0 THEN RETURN
1080 IF D(7)<0 THEN RETURN
1090 PRINT:PRINT"Damage Control report: ";D$(R1);" out.":PRINT:RETURN
1100 R1=INT(RND(0)*8+1):IF D(R1)>=0 THEN 1130
1110 IF D(7)<0 THEN 1130
1120 PRINT:D(R1)=0:PRINT"Damage Control report: ";D$(R1);" repaired.":PRINT
1130 W1=W1*8:A1=8*Q2+S2-9:B1=72-8*Q1-S1:IF W1<1 THEN W1=W1*1.25
1140 E=E-2*W1:T=T+W1/25:IF T>T9 THEN 2520
1150 A2=INT(A1+W1*COS(C1*CX)+.5)
1160 B2=INT(B1+W1*SIN(C1*CX)+.5)
1170 IF NOT((A2<0) OR (A2>63) OR (B2<0) OR (B2>63))THEN 1220
1180 PRINT AR$;"Captain, heed Starfleet regulations! ( STAY IN GALAXY ) !!"
1190 T=T+W1/24:GOTO 710
1220 FOR X=0 TO INT(W1)
1230 IF NOT(Q1=8-INT(FNB1(X)/8)AND Q2=INT(FNA1(X)/8+1))THEN X=W1:GOTO 1400
1240 S3=8-FNB1(X)+8*INT(FNB1(X)/8):S4=FNA1(X)+1-8*INT(FNA1(X)/8)
1250 IF S(S3,S4)<2 THEN 1400
1260 S(S1,S2)=0:S1=8-FNB1(X-1)+8*INT(FNB1(X-1)/8)
1270 S2=FNA1(X-1)+1-8*INT(FNA1(X-1)/8):S(S1,S2)=1: REM don't know about -1 !!
1280 PRINT AR$;"Navigational error :Engines shut down at (";
1290 PRINT S1;","S2;")"
1300 T=T+(W1/24)
1310 PRINT" You lost";T;" units startime restarting engines."
1320 PRINT
1330 IF S(S3,S4)<>4 THEN 710
1340 PRINT"Shields lowered for refueling"
1350 S=0:P=10:C$="D":E=3000-XA+100
1360 GOSUB 2290
1370 FOR R1=1 TO 10
1380 IF D(R1)>=0 THEN NEXT R1:GOTO 710
1390 D(R1)=D(R1)+(11*RND(0)/R9):NEXT R1:GOTO 710
1400 NEXT X:S(S1,S2)=0:S1=8-B2+8*INT(B2/8):S2=A2+1-8*INT(A2/8)
1410 Q3=8-INT(B2/8):Q4=INT(A2/8)+1
1420 IF NOT((Q1=Q3) AND (Q2=Q4))THEN Q1=Q3:Q2=Q4:GOTO 440
1430 S(S1,S2)=1:FOR A=S1-1 TO S1+1:FOR B=S2-1 TO S2+1
1440 IF (A>8) OR (B>8) OR (A<1) OR (B<1) THEN 1460
1450 IF S(A,B)=4 THEN 1340
1460 NEXT B,A:GOTO 710

REM LONG RANGE SCAN

1470 PRINT
1480 FOR A=(Q1-1) TO (Q1+1):FOR B=(Q2-1) TO (Q2+1)
1490 IF (A<1) OR (B<1) OR (A>8) OR (B>8) THEN PRINT" ***";:GOTO 1520
1500 G$ = RIGHT$(STR$(1000+INT(G(A,B))),3)
1510 PRINT " ";G$;
1515 G(A,B)=INT(G(A,B))+0.5
1520 NEXT B:PRINT:NEXT A:PRINT:GOTO 710

REM PHASERS

1530 PRINT
1540 IF K3<=0 THEN 2270
1550 IF D(8)>=0 THEN 1570
1560 PRINT AR$;" Computer failure hampers accuracy"
1570 PRINT"Phasers locked on target. Energy available=";E;
1580 PRINT": Number of units to fire:";
1590 INPUT X
1600 IF E-X<0 THEN PRINT"NOT ENOUGH POWER":GOTO 710
1610 T=T+.05:IF T>T9 THEN 2520
1620 E=E-X
1630 IF C$<>"D"THEN GOSUB 2290
1640 IF D(8)>=0 THEN 1660
1650 X=X*RND(0)
1660 FOR I=1 TO 3
1670 IF K(I,3)<=0 THEN 1770
1680 H=(X/FND(0))+SGN(RND(0)-.5)*8*RND(0)
1690 K(I,3)=K(I,3)-H
1700 PRINT H;" Unit hit on vessel at (";K(I,1);
1710 PRINT K(I,2);"), leaving";K(I,3)
1720 IF K(I,3)>0 THEN 1770
1730 PRINT"Klingon at (";K(I,1);",";K(I,2);
1740 PRINT") destroyed":DEC K3,K9:IF K9=0 THEN 2650
1750 G(Q1,Q2)=G(Q1,Q2)-100:S(K(I,1),K(I,2))=0:K(I,1)=0:K(I,2)=0
1760 K(I,3)=0
1770 NEXT I
1780 GOTO 710

REM PHOTON TORPEDOES

1800 PRINT
1820 IF P=0 THEN PRINT AR$;"All Photon Torpedoes expended":GOTO 710
1830 INPUT"DIRECTION";C1
1840 T=T+.05:IF T>T9 THEN 2520

1850 DEC P:IF C$<>"D" THEN GOSUB 2290
1860 A1=8*Q2+S2-9:B1=72-8*Q1-S1:IF C1<>90*INT(C1/90)THEN 1940

1870 D1=INT(1.1*SIN(C1*CX))
1880 D2=INT(1.1*COS(C1*CX))
1890 FOR W1=0 TO 8
1900 S3=S1-W1*D1
1910 S4=S2+W1*D2
1920 IF (S3>8) OR (S4>8) OR (S3<1) OR (S4<1) THEN 710
1930 GOTO 2000

1940 FOR W1=0 TO 10
1950 IF W1=0 THEN S3=S1:S4=S2:A2=A1:B2=B1:GOTO 2000
1955 S3=FNB1(W1):S4=FNA1(W1)
1960 IF NOT(Q1=8-INT(S3/8) AND Q2=INT(S4/8+1)) THEN 710
1970 S3=8-S3+8*INT(S3/8):S4=S4+1-8*INT(S4/8)
1980 IF (FNA1(W1)=A2) AND (FNB1(W1)=B2) THEN 2120
1990 A2=FNA1(W1):B2=FNB1(W1)
2000 PRINT"(";S3;",";S4;")";A$(S(S3,S4))
2010 ON S(S3,S4)+1 GOTO 2120,2120,2100,2020,2090
2020 IF RND(0)<(.1*X4) THEN PRINT"Shields deflect Torpedo":GOTO 710
2030 PRINT"Klingon destroyed":S(S3,S4)=0:G(Q1,Q2)=G(Q1,Q2)-100
2040 DEC K3,K9:IF K9=0 THEN 2650
2050 FOR A=1 TO 3
2060 IF (K(A,1)=S3) AND (K(A,2)=S4) THEN K(A,1)=0:K(A,2)=0:K(A,3)=0
2070 NEXT A
2080 GOTO 710
2090 PRINT AR$;"Starbase destroyed":B3=0:DEC B9:S(S3,S4)=0:GOTO 2110
2100 PRINT AR$;"You can't destroy a star":GOTO 710
2110 G(Q1,Q2)=G(Q1,Q2)-10:GOTO 710
2120 NEXT W1
2130 GOTO 710

REM SHIELDS

2140 PRINT
2150 PRINT"Energy available=";E+S;": Number of units to Shields";
2160 INPUT X
2170 IF X<0 THEN 710
2180 IF E+S-X<0 THEN 2150
2190 E=E+S-X:S=X
2210 IF SL=1 THEN 660
2220 GOTO 710

REM DAMAGE CONTROL

2230 PRINT
2240 PRINT"DEVICE              STATE OF REPAIR"
2250 FOR R1=1 TO 10:PRINT D$(R1);TAB(21);D(R1):NEXT R1
2260 IF DT=1 THEN 2680
2265 GOTO 710
2270 PRINT"Short Range Sensors report no Klingons in this quadrant"
2280 IF DT=1 THEN 2680
2285 GOTO 710
2290 IF (C$="D") AND K3 THEN PRINT"Starbase protects Enterprise":GOTO 710
2310 PRINT
2320 FOR I=1 TO 3
2330 IF K(I,3)<=0 THEN 2430
2340 H=K(I,3)/FND(0)+SGN(RND(0)-.5)*RND(0)*7
2350 S=S-H:PRINT H;" Unit hit from (";K(I,1);",";
2360 PRINT K(I,2);"), leaving";S
2370 IF S<0 THEN 2550
2380 GOSUB 1050
2390 IF RND(0)>R9/10 THEN 2430
2400 R1=INT(8*RND(0)+1):R2=INT(8*RND(0)+1)
2410 IF NOT(S(R1,R2)=0)THEN 2400
2420 S(R1,R2)=3:S(K(I,1),K(I,2))=0:K(I,1)=R1:K(I,2)=R2
2430 NEXT I
2440 RETURN
2450 PRINT AR$;" Enterprise dead in space"
2460 PRINT
2470 CY=1:PRINT:GOTO 2590
2480 IF K3<=0 THEN 2590
2490 GOSUB 2290
2500 GOTO 2480
2510 PRINT
2520 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT AR$;
2530 PRINT"It is stardate"T:PRINT
2540 GOTO 2590
2550 PRINT
2560 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT AR$;
2570 PRINT"Enterprise disabled !!":PRINT:PRINT
2580 CY=0
2590 PRINT"There are";K9;" Klingons remaining.":IF CY THEN 3560
2600 PRINT"You have failed !!":PRINT
2610 L=INT(RND(0)*5)
2620 ON L+1 GOTO 3440,3470,3490,3510,3540
2630 PRINT:INPUT"Do you want to try again";X$
2640 IF UCASE$(LEFT$(X$,1))="Y" THEN RUN
2645 GOTO 5430
2650 PRINT"The Federation is saved !!"
2660 PRINT"You are hereby promoted to Admiral!!!!":PRINT
2670 GOTO 2630

REM COMPUTER ON

2680 DT=1
2690 INPUT"Computer on--COMMAND";A: IF A=0 THEN 2720
2700 IF (A<0) OR (A>9) THEN PRINT AR$;" NO SUCH COMMAND !!":GOTO 2690
2710 ON A GOTO 2900,2950,3000,2840,600,3400,3420,5120,710
2720 PRINT"FUNCTIONS AVAILABLE FROM COMPUTER"
2730 PRINT"   1 = Galactic Memory Map"
2740 PRINT"   2 = General Status Report"
2750 PRINT"   3 = Photon Trajectory"
2760 PRINT"   4 = New Mission"
2770 PRINT"   5 = Present Position"
2780 PRINT"   6 = Self-Destruct"
2790 PRINT"   7 = Warp-Compass"
2800 PRINT"   8 = Mission Progress Report"
2810 PRINT"   9 = Exit Computer"
2820 GOTO 2680

2830 PRINT:PRINT"--TEMPORARY MALFUNCTION--":GOTO 710

REM NEW MISSION

2840 IF XM<0 THEN 2880
2850 PRINT AR$;"Safety check -- Do you wish to abort mission":INPUT AN$
2860 IF UCASE$(LEFT$(AN$,1))<>"Y" THEN 2890
2865 GOTO 100

2870 PRINT
2880 PRINT"You have not been authorized for new